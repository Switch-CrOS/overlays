# Copyright 2018 The ChromiumOS Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

CROS_WORKON_COMMIT="7351ecf9bce41583b6acdb36defc735770a28f9d"
CROS_WORKON_TREE=("518b50f8b6d01e95cbd933487ed7c6452ac4acb3" "30ef722a1deffdc82e699c9888d67a3c78e4710d" "f91b6afd5f2ae04ee9a2c19109a3a4a36f7659e6")
CROS_WORKON_INCREMENTAL_BUILD=1
CROS_WORKON_LOCALNAME="platform2"
CROS_WORKON_DESTDIR="${S}/platform2"
CROS_WORKON_PROJECT="chromiumos/platform2"
CROS_WORKON_SUBTREE="common-mk diagnostics .gn"

PLATFORM_SUBDIR="diagnostics"

inherit cros-sanitizers cros-workon cros-unibuild platform cros-protobuf udev user

DESCRIPTION="Device telemetry and diagnostics for Chrome OS"
HOMEPAGE="https://chromium.googlesource.com/chromiumos/platform2/+/HEAD/diagnostics"

LICENSE="BSD-Google"
KEYWORDS="*"
IUSE="fuzzer mesa_reven diagnostics dlc"

# switch-t210 fork: the NVIDIA L4T stack exposes no DRM node (/dev/dri), so
# cros_healthd's four DRM/EGL executor probes (display/privacy-screen/graphics)
# bind-mount a nonexistent path into their sandbox; the half-set-up delegate
# then wedges unreapable on the 4.9 kernel ("Failed to kill jail process"),
# stalling the executor and hanging callers -> the Diagnostics app never
# renders. The patch guards those probes on /dev/dri existence and returns the
# normal launch-failure fallback immediately. Drop this once the probes upstream
# handle a DRM-less system gracefully (or if moving to a DRM-capable kernel).
PATCHES=(
	"${FILESDIR}/switch-healthd-skip-drm-probes-without-dev-dri.patch"
)

COMMON_DEPEND="
	acct-user/cros_healthd
	acct-group/cros_healthd
	chromeos-base/bootstat:=
	chromeos-base/chromeos-config-tools:=
	chromeos-base/libec:=
	chromeos-base/metrics:=
	chromeos-base/minijail:=
	chromeos-base/missive:=
	chromeos-base/mojo_service_manager:=
	chromeos-base/spaced:=
	chromeos-base/vboot_reference:=
	dev-libs/glib:=
	dev-libs/libevdev:=
	dev-libs/openssl:=
	dev-libs/re2:=
	net-misc/curl:=
	virtual/libudev:=
	sys-apps/pciutils:=
	virtual/libusb:1=
	virtual/opengles:=
	sys-apps/fwupd:=
	sys-apps/rootdev:=
	sys-apps/util-linux:=
	x11-libs/libdrm:=
"

DEPEND="
	${COMMON_DEPEND}
	chromeos-base/attestation-client:=
	chromeos-base/chromeos-ec-headers:=
	chromeos-base/concierge-client:=
	chromeos-base/cros-camera-libs:=
	chromeos-base/debugd-client:=
	chromeos-base/dlcservice-client:=
	chromeos-base/libiioservice_ipc:=
	chromeos-base/power_manager-client:=
	chromeos-base/session_manager-client:=
	chromeos-base/system_api:=[fuzzer?]
	chromeos-base/tpm_manager-client:=
	media-libs/libcras:=
	net-analyzer/ndt7-client-cc:=
	x11-drivers/opengles-headers:=
"

# TODO(b/271544868): Remove net-wireless/iw once we find alternatives.
RDEPEND="
	${COMMON_DEPEND}
	chromeos-base/crash-reporter
	chromeos-base/debugd
	chromeos-base/iioservice
	dev-util/stressapptest
	net-wireless/iw
	dlc? (
		chromeos-base/fio-dlc
	)
"

BDEPEND="
	chromeos-base/chromeos-dbus-bindings
	chromeos-base/minijail
"

pkg_preinst() {
	enewgroup cros_ec-access
	enewgroup fpdev
	enewuser healthd_ec
	enewgroup healthd_ec
	enewuser healthd_fp
	enewgroup healthd_fp
	enewuser healthd_evdev
	enewgroup healthd_evdev
	enewuser healthd_psr
	enewgroup healthd_psr
	enewgroup mei-access
}

src_install() {
	platform_src_install

	# Install udev rules.
	udev_dorules udev/99-mei_driver_files.rules

	# Install fuzzers.
	local fuzzer_component_id="982097"
	platform_fuzzer_install "${S}"/OWNERS "${OUT}"/fetch_system_info_fuzzer \
		--comp "${fuzzer_component_id}"
	platform_fuzzer_install "${S}"/OWNERS "${OUT}"/crash_events_uploads_log_parser_fuzzer \
		--comp "${fuzzer_component_id}"

	# TODO(b/299052079): Remove mojom file after completing hotline migration.
	insinto /usr/include/cros_healthd-client/diagnostics/mojom/public
	doins "${S}"/mojom/public/cros_healthd_probe.mojom
	doins "${OUT}"/gen/include/diagnostics/mojom/public/cros_healthd_probe.mojom-module
}
